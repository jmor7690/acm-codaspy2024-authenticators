(******************************************************************************
   Remote Registration of Multiple Authenticators
   Yongqi Wang, Thalia Laing, Jose Moreira, Mark Ryan
   ACM CODASPY 2024, June 19-21, 2024, Porto, Portugal

   ProVerif model - Ring authenticators
 ******************************************************************************)


(* Equational theory for probabilistic ring signatures *)
type rsskey.                (* Ring signature secret key *)
type rspkey.                (* Ring signature public key *)
type rsignature.            (* Ring signature *)
fun Pk(rsskey): rspkey.     (* Ring signature public key derivation function *)
type rspkey_list.           (* Ring signature public key list *)
fun PkList({(*$i;*)rspkey}): rspkey_list [data].
                            (* Ring signature public key list constructor *)

type randomness.
fun InternalRingSign(rsskey, rspkey_list, bitstring, randomness): rsignature.

letfun RingSign(xi:rsskey, L:rspkey_list, m:bitstring) =
    new r:randomness; InternalRingSign(xi, L, m, r).

fun RingVerify(rspkey_list, bitstring, rsignature): bool
    reduc
    {forall {x$i;:rsskey}, m:bitstring, r:randomness;
        let L = PkList({Pk(x$i;)}) in
        RingVerify(L, m, InternalRingSign(x$i;, L, m, r)) = true/
    otherwise }
    otherwise forall L:rspkey_list, m:bitstring, s:rsignature;
        RingVerify(L, m, s) = false.


(* Extension to the equational theory for probabilistic ring signatures, to
   model id-specific keypairs, i.e., a dedicated ring signature keypair for each id *)
type id.                                    (* (Public) id, both for users and RPs *)
type shsecret.                              (* Shared secret between authenticators *)
fun DerSk(rsskey, shsecret, id): rsskey.    (* id-specific ring signature secret key derivaion function *)
fun DerPk(rspkey, shsecret, id): rspkey.    (* id-specific ring signature public key derivaion function *)

equation forall xi:rsskey, k:shsecret, RPj:id; DerPk(Pk(xi), k, RPj) = Pk(DerSk(xi, k, RPj)).
                                            (* Equation relating key derivation function and id-specific key derivation functions *)


(* Other types and tables needed *)
type tag.           (* Protocol message tag *)
const REG:tag.      (* Registration protocol message tag *)
const AUTH:tag.     (* Authentication protocol message tag *)
type challenge.     (* RP challenge *)
free c:channel.
table registered_auth(id, rspkey_list).     (* Stores registered authenticators - relying party store *)


(********************
  Events and queries
 ********************)

event A_Register(id, nat, rsskey, rspkey_list, id).             (* uid, i, xi, Lj,   RPj, -- *)
event R_Register(id, rspkey_list).                              (* --, --, --, RPj, Lj,   -- *)

event A_Running(id, nat, rsskey, rspkey_list, id, bitstring).   (* uid, i, xi, Lj,   RPj, p  *)
event R_Commit(id, rspkey_list, bitstring).                     (* --, --, --, RPj, Lj,   p  *)


(* SANITY-CHECK. It is reachable that all the authenticators belonging to a user
   uid cast the same list of public keys to register with RPj *)
query uid:id, RPj:id, {x$i;:rsskey}, k:shsecret;
    let Lj = PkList({Pk(DerSk(x$i;, k, RPj))}) in
    {event(A_Register(uid, $i;, x$i;, Lj, RPj))/ &&
    }.


(* PROPERTY 1 (G1, G2, G7, G8, R4). All authenticators of user uid register the
   same list of public keys with RPj. *)
query uid:id, RPj:id,
    i:nat, xi:rsskey, Lj:rspkey_list;
    event(A_Register(uid, i, xi, Lj, RPj)).

query uid:id, RPj:id,
    i:nat, xi:rsskey, Lj:rspkey_list,
    i':nat, xi':rsskey, Lj':rspkey_list;
    event(A_Register(uid, i, xi, Lj, RPj)) &&
    event(A_Register(uid, i', xi', Lj', RPj))
    ==> Lj=Lj'.


(* PROPERTY 2 (G1, G7, G8, R1, R3). All authenticators of user uid register a
   given list of public keys with at most one RP *)
query uid:id, RPj:id,
    i:nat, xi:rsskey, Lj:rspkey_list;
    event(A_Register(uid, i, xi, Lj, RPj)).

query uid:id, RPj:id, RPj':id,
    i:nat, xi:rsskey, Lj:rspkey_list,
    i':nat, xi':rsskey;
    event(A_Register(uid, i, xi, Lj, RPj)) &&
    event(A_Register(uid, i', xi', Lj, RPj'))
    ==> RPj=RPj'.


(* PROPERTY 3 (G1, G2, G7, G8, R4). Is it reachable that an authenticator of user
   uid registers all their authenticators, and any other authenticator of uid can
   then log in.

   We need three different queries because once one authenticator of uid registers
   to RPj, the remaining authenticators will not register again for RPj.
   To test this property correctly, each of the three queries below must be
   tested individually by enabling the necessary restrictions to prevent other
   authenticators to register (e.g., for 3 authenticators, in order to prove the
   query where x1 registers, the restrictions for x2 and x3 must be enabled).

   However, ProVerif is able to find traces satisfying the restrictions, without
   having to enable them. *)
(*
{restriction uid:id, {x$i;:rsskey}, k:shsecret, RPj:id;
    let Lj = PkList({Pk(DerSk(x$i;, k, RPj))}) in
    event(A_Register(uid, $i;, x$i;, Lj, RPj))./

}
*)
{query uid:id, {x$i;:rsskey}, k:shsecret, RPj:id, {p$i;:bitstring};
    let Lj = PkList({Pk(DerSk(x$i;, k, RPj))}) in
    event(A_Register(uid, $i;, x$i;, Lj, RPj)) &&
    {event(A_Running(uid, $i;, x$i;, Lj, RPj, p$i;)) && event(R_Commit(RPj, Lj, p$i;))/ &&
    }./

}


(* PROPERTY 4 (Injective agreement, G6, G7, G8, R3): Whenever RPj completes a
   run of the authentication protocol, apparently with a registered authenticator
   belonging to user uid (and whose public key is in Lj), then an authenticator
   belonging to user uid has previously been running the protocol, apparently
   with relying party RPj, and the two agents agreed on the data values
   corresponding to all the variables in p, and each such run of the protocol
   on RPj corresponds to a unique run of the protocol on the authenticator. *)
query uid:id, i:nat, xi:rsskey, Lj:rspkey_list, RPj:id, p:bitstring;
    event(A_Register(uid, i, xi, Lj, RPj)) && event(R_Register(RPj, Lj)) && event(R_Commit(RPj, Lj, p)).

query uid:id, i:nat, xi:rsskey, Lj:rspkey_list, i':nat, xi':rsskey, RPj:id, p:bitstring;
    event(A_Register(uid, i, xi, Lj, RPj)) && inj-event(R_Commit(RPj, Lj, p))
    ==> inj-event(A_Running(uid, i', xi', Lj, RPj, p)).
query uid:id, i:nat, xi:rsskey, Lj:rspkey_list, i':nat, xi':rsskey, RPj:id, p:bitstring;
    event(A_Register(uid, i, xi, Lj, RPj)) && event(R_Register(RPj, Lj)) && inj-event(R_Commit(RPj, Lj, p))
    ==> inj-event(A_Running(uid, i', xi', Lj, RPj, p)).


(***********************
  Relying party process
 ***********************)
let RelyingParty_Register(RPj:id) =
    new regChal:challenge;
    out(c, (RPj, regChal));
    in(c, (Lj:rspkey_list, xS:rsignature));
    if RingVerify(Lj, (REG, regChal), xS) = true then
    insert registered_auth(RPj, Lj);
    event R_Register(RPj, Lj).

let RelyingParty_Authenticate(RPj:id) =
    get registered_auth(=RPj, Lj) in
    new authChal:challenge;
    out(c, (RPj, authChal));
    in(c, xS:rsignature);
    if RingVerify(Lj, (AUTH, authChal), xS) = true then
    event R_Commit(RPj, Lj, (Lj, RPj, authChal, xS)).

let RelyingParty() =
    new RPj:id;
    out(c, RPj);
    !RelyingParty_Register(RPj)
    | !RelyingParty_Authenticate(RPj).


(***********************
  Authenticator process
 ***********************)
let Authenticator_Register(uid:id, i:nat, xi:rsskey, L:rspkey_list, k:shsecret) =
    in(c, (xRPj:id, xRegChal:challenge));
    let xij = DerSk(xi, k, xRPj) in
    let PkList({y$i;}) = L in
    {let y$i;j = DerPk(y$i;, k, xRPj) in/
    }
    let Lj = PkList({y$i;j}) in
    let s = RingSign(xij, Lj, (REG, xRegChal)) in
    event A_Register(uid, i, xi, Lj, xRPj);
    out(c, (Lj, s)).

let Authenticator_Authenticate(uid:id, i:nat, xi:rsskey, L:rspkey_list, k:shsecret) =
    in(c, (xRPj:id, xAuthChal:challenge));
    let xij = DerSk(xi, k, xRPj) in
    let PkList({y$i;}) = L in
    {let y$i;j = DerPk(y$i;, k, xRPj) in/
    }
    let Lj = PkList({y$i;j}) in
    let s = RingSign(xij, Lj, (AUTH, xAuthChal)) in
    event A_Running(uid, i, xi, Lj, xRPj, (Lj, xRPj, xAuthChal, s));
    out(c, s).

let Authenticator(uid:id, i:nat, xi:rsskey, L:rspkey_list, k:shsecret) =
    !Authenticator_Authenticate(uid, i, xi, L, k)
    | !Authenticator_Register(uid, i, xi, L, k).


(*************************************
  Authenticator set generator process
 *************************************)
let AuthSetGenerator() =
    new uid:id;
    out(c, uid);

    {new x$i;:rsskey;/
    }
    {let y$i; = Pk(x$i;) in/
    }
    let L = PkList({y$i;}) in
    out(c, L);

    new k:shsecret;
    {Authenticator(uid, $i;, x$i;, L, k)/
    | }.


(**************
  MAIN PROCESS
 **************)
process
    !AuthSetGenerator()
    | !RelyingParty()
